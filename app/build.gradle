plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "com.google.dagger.hilt.android"
    id "kotlin-kapt"
}

android {
    def PACKAGE_NAME = "com.w2sv.wifiwidget"
    def TARGET_SDK = 33

    namespace PACKAGE_NAME
    compileSdk TARGET_SDK

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdk 26
        targetSdk TARGET_SDK
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        // create release signingConfig from keystore.properties file, if present
        release {
            rootProject.file("keystore.properties").with { file ->
                if (file.exists()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(file))

                    storeFile rootProject.file("keys.jks")
                    storePassword keystoreProperties["storePassword"]
                    keyAlias keystoreProperties["keyAlias"]
                    keyPassword keystoreProperties["keyPassword"]
                } else
                    println("WARNING: Failed to create release signing configuration; $file.path not present")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false  // TODO: figure out why altering colors when set to true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'com.github.w2sv:TypedPreferences:feature~lifecycle-observer-SNAPSHOT'
    implementation 'com.github.PaulWoitaschek:Slimber:2.0.0'

    // hilt
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-android-compiler:2.42"

    // compose
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material:1.4.0-alpha02'  // TODO: Remove as soon as BottomSheet available for material3
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}